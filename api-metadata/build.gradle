plugins {
  id "io.forgo.keystoreplugin" version "1.1"
}

apply plugin: 'war'
apply plugin: 'groovy'
apply plugin: 'org.springframework.boot' // version declared in top-level buildscript
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.cedar.dockerplugin'
apply from: "${rootDir}/gradle/publishing.gradle"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()

  // opensaml repo
  maven { url "https://build.shibboleth.net/nexus/content/repositories/releases" }
  // avro serdes from confluent
  maven { url 'https://packages.confluent.io/maven/' }
  // for shared avro schemas from https://github.com/cedardevs/schemas
  maven { url "https://jitpack.io" }
}

dependencies {
  implementation(project(':elastic-common'))
  testImplementation(project(path: ':elastic-common', configuration: 'testElastic'))

  // Kafka dependencies for connecting with PSI
  implementation('com.github.cedardevs.schemas:schemas-core:master-SNAPSHOT')
  implementation('com.github.cedardevs.schemas:schemas-parse:master-SNAPSHOT')
  implementation('com.github.cedardevs.schemas:schemas-analyze:master-SNAPSHOT')
  implementation("io.confluent:kafka-streams-avro-serde:5.1.0")

  implementation("org.apache.kafka:kafka-clients:2.1.1")
  implementation("org.springframework.kafka:spring-kafka:2.2.5.RELEASE")

  testImplementation("com.github.cedardevs.schemas:schemas-core:master-SNAPSHOT:test")

  testImplementation("io.confluent:kafka-schema-registry:5.1.0")
  testImplementation("io.confluent:kafka-schema-registry:5.1.0:tests")

  testImplementation("org.apache.kafka:kafka_2.11:2.1.1")
  testImplementation("org.apache.kafka:kafka_2.11:2.1.1:test")
  testImplementation("org.apache.kafka:kafka-clients:2.1.1:test")
  implementation("org.springframework.kafka:spring-kafka-test:2.2.5.RELEASE")

  // opensaml
  implementation("org.opensaml:opensaml-core:${project.openSAMLVersion}")
  implementation("org.opensaml:opensaml-saml-api:${project.openSAMLVersion}")
  implementation("org.opensaml:opensaml-saml-impl:${project.openSAMLVersion}")
  implementation("org.opensaml:opensaml-messaging-api:${project.openSAMLVersion}")
  implementation("org.opensaml:opensaml-messaging-impl:${project.openSAMLVersion}")
  implementation("org.opensaml:opensaml-soap-api:${project.openSAMLVersion}")
  implementation("org.opensaml:opensaml-soap-impl:${project.openSAMLVersion}")

  // logback
  implementation("ch.qos.logback:logback-core:${project.logbackVersion}")
  implementation("ch.qos.logback:logback-classic:${project.logbackVersion}")

  // javax servlet
  implementation("javax.servlet:javax.servlet-api:${project.javaxServletAPIVersion}")

  // junit
  testImplementation("junit:junit:${project.junitVersion}")
  testImplementation("org.springframework.security:spring-security-test")


  implementation "org.codehaus.groovy:groovy:${project.groovyVersion}"
  implementation "org.codehaus.groovy:groovy-json:${project.groovyVersion}"
  implementation "org.codehaus.groovy:groovy-xml:${project.groovyVersion}"
  implementation "org.elasticsearch.client:elasticsearch-rest-client:${project.esVersion}"
  implementation "org.elasticsearch.client:transport:${project.esVersion}"
  implementation "org.springframework.boot:spring-boot-starter-web"
  implementation "org.springframework.boot:spring-boot-starter-actuator"
  implementation "org.springframework.boot:spring-boot-starter-security"
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation "org.springframework.boot:spring-boot-devtools"
  implementation 'com.github.java-json-tools:json-schema-validator:2.2.8'
  implementation 'org.apache.commons:commons-text:1.2'
  implementation 'org.apache.lucene:lucene-expressions:7.2.1'

  // Authorization Services
  implementation "org.springframework.boot:spring-boot-starter-data-jpa"
  runtime group: 'org.hsqldb', name: 'hsqldb', version: '2.4.1'
  // version to override another library's dependency to solve a CVE:
  implementation 'com.google.guava:guava:27.0.1-jre'
  implementation 'commons-collections:commons-collections:3.2.2'

  // fix CVE-2019-0232, CVE-2019-0232 by forcing a higher version of the tomcat websocket dependency
  providedRuntime group: 'org.apache.tomcat.embed', name: 'tomcat-embed-websocket', version: '9.0.19'
  providedRuntime group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: '9.0.19'

  runtime 'org.springframework.boot:spring-boot-devtools'

  providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

  testImplementation "org.spockframework:spock-spring:${project.spockVersion}"
  testImplementation "org.spockframework:spock-core:${project.spockVersion}"
  testImplementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation "org.testcontainers:testcontainers:1.10.7"
  testImplementation "org.testcontainers:elasticsearch:1.10.7"
  testImplementation 'net.bytebuddy:byte-buddy:1.7.11'
}

keystore {
  // resetOutputDir task defaults
  outputDir = ".keystore"

  // sslKey task defaults
  keyFile = "debug.key"
  keyPassword = "password"

  // sslCert task defaults (depends on: sslKey)
  certFile = "debug.crt"

  // pkcs12 task defaults (depends on: sslCert)
  pkcs12File = "debug.pkcs12"
  pkcs12Password = "password"

  // jks task defaults (depends on: pkcs12)
  jksFile = "debug.jks"
  jksPassword = "password"
  alias = "debug"
}

// Example of running Bootrun with a debug keystore generated with `./gradlew api-metadata:jks`
//export SPRING_PROFILES_ACTIVE="icam,manual-upload" && \
// export SCIAPPS_KEYSTORE_ALIAS="debug" && \
// export SCIAPPS_KEYSTORE_PASSWORD="password" && \
// export SCIAPPS_KEY_PASSWORD="password" && \
// cp ~/Documents/GitHub/onestop/api-metadata/.keystore/debug.jks /etc/api-metadata/keystore.jks && \
// ./gradlew api-metadata:bootrun


ext {
  fatWarPath = file("${buildDir}/libs/${rootProject.name}-${project.name}-all.war")
}

war {
  enabled = true
  // TODO disabled dynamic project-based name for #827, plan to re-enable in #828
  // archiveBaseName.set("${rootProject.name}-${project.name}")
  archiveBaseName.set("onestop-metadata")
  // If the `archiveFileName` has not been explicitly set,
  // the pattern for the name is:
  // "${archiveBaseName}-${archiveAppendix}-${archiveVersion}-${archiveClassifier}.${archiveExtension}"
  archiveFileName.set("${archiveBaseName.get()}.${archiveExtension.get()}")

  finalizedBy bootWar
}

bootWar {
  excludeDevtools = true
  archiveClassifier.set("all")
  launchScript()
  onlyIf { !tasks.getByName('war').state.upToDate }
}

// Integration Test Build References:
// http://griffon-framework.org/tutorials/6_minimalistic_build.html
// https://spin.atomicobject.com/2018/07/18/gradle-integration-tests/
configurations {
  // fix for vulnerability warning
  implementation.exclude module: 'tomcat-annotations-api', group: 'org.apache.tomcat'

  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

sourceSets {
  integrationTest {
    compileClasspath += sourceSets.main.output + sourceSets.test.output
    runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    groovy.srcDir file('src/integrationTest/groovy')
    resources.srcDir file('src/integrationTest/resources')
  }
}

task integrationTest(type: Test) {
  description = 'Runs the integration tests'
  group = 'verification'
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  outputs.upToDateWhen { false }
  shouldRunAfter test

  reports.junitXml.destination = file("${buildDir}/integration-test-results")
  reports.html.destination = file("${buildDir}/reports/integrationTests")
}
check.dependsOn integrationTest

task sourceJar(type: Jar) {
  archiveClassifier.set("sources")
  archiveBaseName.set("${rootProject.name}-${project.name}")
  from "${projectDir}/src"
}

build.dependsOn sourceJar

publishing {
  publications {
    main(MavenPublication) {
      artifact war
      artifactId "${rootProject.name}-${project.name}"
      artifact tasks.sourceJar
    }
  }
}

processResources {
  outputs.upToDateWhen { false }
  filesMatching("**/application*.yml") {
    expand([
        esVersion: project.esVersion,
        version  : project.version,
        buildDate: project.buildDate,
    ])
  }
}

docker {
  additionalBuildArgs = [WAR_NAME: bootWar.outputs.files.singleFile.name]
}

BuildDockerImage.dependsOn(bootWar)
// assemble.dependsOn(BuildDockerImage)
PublishDockerImage.dependsOn(BuildDockerImage)
publish.dependsOn PublishDockerImage

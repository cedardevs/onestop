buildscript {
  ext {
    springBootVersion = '1.5.8.RELEASE'
  }
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath 'com.gradle:build-scan-plugin:1.9'
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.1'
    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.0.0"
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    classpath 'com.wiredforcode:gradle-spawn-plugin:0.6.0'
    classpath 'com.moowork.gradle:gradle-node-plugin:0.13'
    classpath 'org.owasp:dependency-check-gradle:2.1.1'
    classpath "gradle.plugin.org.cedar:dockerplugin:1.0.1"
  }
}

ext {
  esVersion = '5.5.3'
  isRelease = System.getenv('TRAVIS_TAG')?.startsWith('v') as Boolean
}

subprojects {
  project.ext {
    esVersion = "${project.esVersion}"
    groovyVersion = '2.4.12'
    spockVersion = '1.1-groovy-2.4'
  }

  afterEvaluate {
    if (project.plugins.hasPlugin('org.cedar.dockerplugin')) {
      docker {
        username = System.getenv('DOCKER_USER')
        password = System.getenv('DOCKER_PASSWORD')
      }
    }

    if (project.plugins.hasPlugin('java')) {
      project.apply plugin: 'org.owasp.dependencycheck'

//      Not functioning in travis, disconnecting temporarily to move forward
//      project.tasks.findByName('check').dependsOn('dependencyCheck')

      dependencyCheck {
        skipConfigurations = ["providedRuntime"]
        suppressionFile = "${rootDir}/owasp-suppressions.xml"
        failBuildOnCVSS = 4

        // One of our dependencies has an un-parsable pom which causes dependency-checker
        // to throw an exception. However, the checks still run and it still generates a
        // report, so I think it's safe(ish) to ignore the error.
        failOnError = false
      }
    }
  }
}

task publish(type: Exec, description: 'Promotes artifactory snapshot', group: 'publish') {
  onlyIf { isRelease }
  executable = "bash"
  args = ["-c", "curl -X POST -u \"\$BINTRAY_USER:\$BINTRAY_KEY\" -H \"Content-Length: 0\"" +
    "http://oss.jfrog.org/api/plugins/build/promote/snapshotsToBintray/${rootProject.name}/\$TRAVIS_BUILD_NUMBER?params=releaseVersion=$version\""]
}

apply plugin: 'com.gradle.build-scan'

group = 'org.cedar.onestop'

subprojects.each {
  it.group = group
  it.version = version
  it.ext.buildDate = "${new Date().format("YYYY-MM-dd", TimeZone.getTimeZone('UTC'))}"
}

buildScan {
  licenseAgreementUrl = 'https://gradle.com/terms-of-service'
  licenseAgree = 'yes'
}

task dockerComposeStart(type: Exec, description: 'Runs the entire system in docker containers', group: 'docker') {
  dependsOn ":api-search:build", ":api-metadata:build", ":client:build", "geoportal-search:build"
  executable = "bash"
  args = ["-c", "ES_VERSION=$project.esVersion docker-compose up -d"]
}

task dockerComposeStop(type: Exec, description: 'Stops the entire system in docker containers', group: 'docker') {
  executable = "bash"
  args = ["-c", "docker-compose stop"]
}

task dockerComposeInfraStart(type: Exec, description: 'Runs infrastructure needed under our apps, e.g. elasticsearch', group: 'docker') {
  executable = "bash"
  args = ["-c", "ES_VERSION=$project.esVersion docker-compose up -d elasticsearch"]
}

task dockerComposeInfraStop(type: Exec, description: 'Stops the infrastructure needed under our apps, e.g. elasticsearch', group: 'docker') {
  executable = "bash"
  args = ["-c", "docker-compose stop elasticsearch"]
}

task dockerComposeBackendStart(type: Exec, description: 'Runs intrastructure and api modules needed to run the client.', group: 'docker') {
  dependsOn ":api-search:build", ":api-metadata:build", ":dockerComposeInfraStart"
  executable = "bash"
  args = ["-c", "ES_VERSION=$project.esVersion docker-compose up -d api-metadata api-search"]
}

task dockerComposeBackendStop(type: Exec, description: 'Stops intrastructure and api modules needed to run the client.', group: 'docker', dependsOn: [":dockerComposeInfraStop"]) {
  executable = "bash"
  args = ["-c", "docker-compose stop api-metadata api-search"]
}

task dockerComposeClean(type: Exec, description: 'Stops and removes the entire system of docker containers', group: 'docker') {
  executable = "bash"
  args = ["-c", "docker-compose down"]
}

task clean {
  dependsOn dockerComposeClean
}

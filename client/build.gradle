apply plugin: "com.moowork.node"
apply plugin: 'org.cedar.dockerplugin'
apply from: "${rootDir}/gradle/publishing.gradle"

node {
    version = '10.15.3'
    download = true
}

clean {
    doFirst {
      npm_clean
    }
}

task npm_clean(type: Exec) {
  executable = "bash"
  args = ["-c", "npm run clean"]
}

def sourceDirs = ['node_modules', 'img', 'src']

task compile(type: NodeTask) {
    dependsOn 'npmInstall'
    sourceDirs.each {
        inputs.dir(it).skipWhenEmpty()
    }
    outputs.dir("${buildDir}/dist")
    script = file('node_modules/.bin/webpack')
    args = ['--bail', '-p']
    execOverrides {
        it.environment 'NODE_ENV', 'production'
    }
    doFirst {
        // Remove previous outputs. Doesn't run if the task if UP-TO-DATE
        file("${buildDir}/dist").deleteDir()
    }
}

task start(type: Exec) {
  executable = "bash"
  args = ["-c", "npm run dev"]
}

task tarResults(type: Tar) {
  dependsOn 'compile'

  from "${buildDir}/dist"
  archiveBaseName.set("onestop-client")
  archiveExtension.set("tar.gz")
  version = '' //project.version

  // If the `archiveFileName` has not been explicitly set,
  // the pattern for the name is:
  // "${archiveBaseName}-${archiveAppendix}-${archiveVersion}-${archiveClassifier}.${archiveExtension}"
  archiveFileName.set("${archiveBaseName.get()}.${archiveExtension.get()}")

  setCompression(Compression.GZIP)
  destinationDirectory = file("${buildDir}/libs")
}

assemble.dependsOn 'tarResults'

npm_test {
    dependsOn 'compile'
    sourceDirs.each {
        inputs.dir(it).skipWhenEmpty()
    }
    inputs.dir('test').skipWhenEmpty()
}

task formatCheck (type: Exec) {
  executable = "bash"
  args = ["-c", "npm run formatCheck"]
}

task test {
    dependsOn 'npm_test'
}

task retire(type: Exec) {
  executable = "bash"
  args = ["-c", "npm run retire"]
}

check {
    dependsOn 'test'
    dependsOn 'retire'
    dependsOn 'formatCheck'
}

build {
    dependsOn 'assemble', 'check', 'sourceJar'
}

task sourceJar(type: Jar) {
  archiveClassifier.set("sources")
  archiveBaseName.set("${rootProject.name}-${project.name}")
  from "${projectDir}/src"
}

publishing {
    publications {
        main(MavenPublication) {
            artifact tarResults.outputs.files.singleFile, {
                extension 'tar.gz'
            }
            artifactId "${rootProject.name}-${project.name}"
            artifact tasks.sourceJar
        }
    }
}

docker {
  additionalBuildArgs = [TAR_NAME: tarResults.outputs.files.singleFile.name]
}

BuildDockerImage.dependsOn(tarResults)
// assemble.dependsOn(BuildDockerImage)
PublishDockerImage.dependsOn(BuildDockerImage)
publish.dependsOn PublishDockerImage

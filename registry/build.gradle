plugins {
  id 'war'
  id 'groovy'
  id 'jacoco'
  id "org.springframework.boot" version "2.1.1.RELEASE"
  id "io.spring.dependency-management" version "1.0.6.RELEASE"
  id 'org.cedar.dockerplugin' version '1.0.3'
  id "com.moowork.node"
}

configurations {
  compile.exclude module: 'tomcat-annotations-api', group: 'org.apache.tomcat'
  testCompile.exclude(group: 'ch.qos.logback')

  integrationTestCompile.extendsFrom testCompile
  integrationTestRuntime.extendsFrom testRuntime
  
  schemaDefinitions
}

dependencies {
  compile('org.codehaus.groovy:groovy')
  compile('org.codehaus.groovy:groovy-json')
  compile('org.springframework.boot:spring-boot-starter-actuator')
  compile('org.springframework.boot:spring-boot-starter-web')
  compile('org.apache.kafka:kafka-streams:2.1.0')
  compile('org.apache.kafka:kafka-clients:2.1.0')
  compile('io.confluent:kafka-streams-avro-serde:5.1.0')
  compile(project(':common'))
  //configure  to unzip and copy schema definitions 
  schemaDefinitions ('com.github.cedardevs:schemas:5-inputSchemas-SNAPSHOT')

  compileOnly('org.springframework.boot:spring-boot-starter-tomcat')

  runtime('org.springframework.boot:spring-boot-devtools')

  testCompile('org.springframework.boot:spring-boot-starter-test')
  testCompile("org.spockframework:spock-spring:${project.spockVersion}")
  testCompile("org.spockframework:spock-core:${project.spockVersion}")
  testCompile('org.codehaus.groovy.modules.http-builder:http-builder:0.7.1')
  testCompile('org.springframework.boot:spring-boot-starter-test')
  testCompile('org.springframework.kafka:spring-kafka-test:2.2.3.RELEASE')
  testCompile('org.apache.kafka:kafka-streams-test-utils:2.1.0')
  testCompile('org.apache.kafka:kafka-clients:2.1.0:test')
  testCompile('org.apache.kafka:kafka_2.12:2.1.0')
  testCompile('org.apache.kafka:kafka_2.12:2.1.0:test')
  testCompile('io.confluent:kafka-schema-registry:5.1.0')
  testCompile('io.confluent:kafka-schema-registry:5.1.0:tests')
  testCompile('com.github.java-json-tools:json-schema-validator:2.2.10')
  testCompile('com.github.cedardevs:schemas:5-inputSchemas-SNAPSHOT:test')
}

sourceSets {
  integrationTest {
    groovy.srcDir file('src/integrationTest/groovy')
//    resources.srcDir file("src/main/resources")
    compileClasspath += main.output + test.output + configurations.testRuntime
    runtimeClasspath += output + compileClasspath
  }
}

task integrationTest(type: Test, description: 'Runs the integration tests', group: 'verification') {
  mustRunAfter 'test'
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  reports.junitXml.destination = file("${buildDir}/integration-test-results")
  reports.html.destination =  file("${buildDir}/reports/integrationTests")
}

check.dependsOn integrationTest

war {
  dependsOn { 'generateOpenApi' }
  enabled = true // build a deployable war in addition to the executable one
  classpath -= files(configurations.compileOnly)
  baseName = "${rootProject.name}-${project.name}"
}

bootWar {
  // we're building a regular war and bootable jar, don't need a bootable war as well
  enabled = false
}

bootJar {
  dependsOn { 'generateOpenApi' }
  classifier = 'all' // add a classifier to distinguish the executable jar
  baseName = "${rootProject.name}-${project.name}"
  launchScript()
}

task sourceJar(type: Jar) {
  classifier = 'sources'
  baseName = "${rootProject.name}-${project.name}"
  from "${projectDir}/src"
}

task extractSchemaDefinitions(type: Copy) {
  dependsOn configurations.schemaDefinitions
  from { // use of closure defers evaluation until execution time
    configurations.schemaDefinitions.collect {
      zipTree(it).matching{
        include 'json/schema-definitions.json'
      }
    }
  }

  into "$buildDir/resources/main"
}

node {
  version = '10.15.3'
  npmVersion = '6.1.0'
  download = true
}

// NOTE: The 'build' task defined in package.json pipes the output directly to the output file
task generateOpenApi(type: NpmTask) {
  dependsOn { ['npmInstall', 'extractSchemaDefinitions', 'processResources'] }
  args = ['run', 'build']
  outputs.file("$buildDir/resources/main/static/openapi.yaml")
}

docker {
  additionalBuildArgs = [JAR_NAME: bootJar.outputs.files.singleFile.name]
}

BuildDockerImage.dependsOn(bootJar)
assemble.dependsOn(BuildDockerImage)
assemble.dependsOn(sourceJar)
assemble.dependsOn(bootJar)
PublishDockerImage.dependsOn(BuildDockerImage)
task publish { dependsOn PublishDockerImage }
